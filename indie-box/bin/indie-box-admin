#!/usr/bin/perl
#
# Central administration script for Indie Box Project administration
#
# Copyright (C) 2013 Indie Box Project http://indieboxproject.org/
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#

use strict;
use warnings;

use IndieBox::AdminUtils;
use IndieBox::Utils;

my $commands = IndieBox::AdminUtils::findCommands();

unless( @ARGV ) {
    synopsisHelpQuit();
}
my $command = shift @ARGV;
if( '--help' eq $command || '-h' eq $command ) {
    synopsisHelpQuit( 1 );
}
my $commandPackage = $commands->{$command};

if( $commandPackage ) {
    foreach my $arg ( @ARGV ) {
        if( '--help' eq $arg || '-h' eq $arg ) {
            if( $commandPackage ) {
                synopsisHelpQuit( 1, $command );
            } else {
                synopsisHelpQuit( 1 );
            }
        }
    }

    # May not be interrupted, bad things may happen if it is
    $SIG{'HUP'}  = 'IGNORE';
    $SIG{'INT'}  = 'IGNORE';
    $SIG{'QUIT'} = 'IGNORE';
    
    my $result = IndieBox::Utils::invokeMethod( $commandPackage . '::run', @ARGV );

    exit !$result;

} else {
    synopsisHelpQuit();
}

#####
# Print synopsis or help
# $long: if true, print long help
# $helpFor: if given, only provide help on this command
sub synopsisHelpQuit {
    my $long    = shift;
    my $helpFor = shift;

    unless( $helpFor ) {
        if( $long ) {
            print <<END;
The central administration script for the Indie Box Project. It enables
the deployment and undeployment of web apps with a single command, as
well as related administration tasks. For more information about the
Indie Box Project, see http://indieboxproject.org/

It may be invoked in the following ways:

END
        } else {
            print "Synopsis:\n";
        }
    }

    foreach my $command ( sort keys %$commands ) {
        if( !$helpFor || $command eq $helpFor ) {
            my $package      = $commands->{$command};
            my $synopsisHelp = IndieBox::Utils::invokeMethod( $package . '::synopsisHelp' ) || '';
            
            while( my( $synopsis, $help ) = each %$synopsisHelp ) {
                $synopsis =~ s!^\s+!!;
                $synopsis =~ s!\s+$!!;

                print "    $0 $command" . ( $synopsis ? " $synopsis" : '' ) . "\n";
                if( $long || $helpFor ) {
                    $help =~ s!^\s+!!gm;
                    $help =~ s!\s+$!!gm;
                    $help =~ s!^!        !gm;

                    print "$help\n\n";
                }
            }
        }
    }

    unless( $helpFor ) {
        print "    $0 --help\n";
        if( $long ) {
            print <<END;
        Display help text.

END
        }
        print "    $0 <command> [<args>...] --help\n";
        if( $long ) {
            print <<END;
        Display help text for this command.

END
        }
    }

    exit 0;
}

1;
